plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.intershop.gradle.jaxb' version '4.3.1'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def lombokVersion = '1.18.12'
def log4jVersion = '2.13.3'

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from(sourceSets.main.allJava)
}

def xsdPath = "${rootDir}/src/main/resources/xsd"
def mainPath = "${rootDir}/src/main/java/"

jaxb {
     javaGen {
         consultaSit {
             outputDir = file(mainPath)
             packageName = 'br.com.dfe.schema'
             binding = file("${rootDir}/src/main/xjb/bindings.xjb")
             schema = file("${xsdPath}/nfe/leiauteConsSitNFe_v4.00.xsd")
         }

         outros {
             outputDir = file(mainPath)
             packageName = 'br.com.dfe.schema'
             binding = file("${rootDir}/src/main/xjb/bindings.xjb")
             schemas = fileTree(dir: "${xsdPath}/nfe", includes: ['procNFe_v4.00.xsd', 'leiauteConsStatServ_v4.00.xsd', 'leiauteInutNFe_v4.00.xsd', 'leiauteNFe_v4.00.xsd'])
         }

         cce {
             outputDir = file(mainPath)
             packageName = 'br.com.dfe.schema.cce'
             binding = file("${rootDir}/src/main/xjb/bindings.xjb")
             schemas = fileTree(dir: "${xsdPath}/eventoCCe", include: '**/*.xsd')
         }

         cancelamento {
             outputDir = file(mainPath)
             packageName = 'br.com.dfe.schema.canc'
             binding = file("${rootDir}/src/main/xjb/bindings.xjb")
             schemas = fileTree(dir: "${xsdPath}/eventoCanc", include: '**/*.xsd')
         }

         cancelamentoSubst {
             outputDir = file(mainPath)
             packageName = 'br.com.dfe.schema.cancsubst'
             binding = file("${rootDir}/src/main/xjb/bindings.xjb")
             schemas = fileTree(dir: "${xsdPath}/eventoCancSubst", include: '**/*.xsd')
         }

         eventoGenerico {
             outputDir = file(mainPath)
             packageName = 'br.com.dfe.schema.generico'
             binding = file("${rootDir}/src/main/xjb/bindings.xjb")
             schemas = fileTree(dir: "${xsdPath}/eventoGen", include: '**/*.xsd')
         }
    }
}

clean {
    delete "${mainPath}/br/com/dfe/schema"
    delete "${rootDir}/src/main/java/META-INF/"
}

group = 'br.com.dfe'
version = '1.02'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(sourcesJar)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}